\begin{elpicode}

  kind to type.
  type o-app list to -> to.
  type o-lam (to -> to) -> to.
  type o-con string -> to.
  type o-uva addr -> to.

  typeabbrev fsubst (mem to).

  type fder fsubst -> to -> to -> o.
  fder _ (o-con C) (o-con C).
  fder S (o-app A) (o-app B) :- map (fder S) A B.
  fder S (o-lam F) (o-lam G) :-
    pi x\ fder S x x => fder S (F x) (G x).
  fder S (o-uva N) R :- set? N S T, fder S T R.
  fder S (o-uva N) (o-uva N) :- unset? N S.

  type fderef fsubst -> to -> to -> o.                 ~($\customlabel{pred:fder}{\rho s}$)~
  fderef S T T2 :- fder S T T1, napp T1 T2.


  ~\PYG{k+kd}{type} \PYG{n+nf}{(\Eo)} \PYG{k+kt}{to -> to -> o}~.                              ~($\customlabel{pred:fequal}{\Eo}$)~
  o-con X ~$\Eo$~ o-con X.
  o-app A ~$\Eo$~ o-app B :- forall2 (~$\Eo$~) A B.
  o-lam F ~$\Eo$~ o-lam G :- pi x\ x ~$\Eo$~ x => F x ~$\Eo$~ G x.    ~($\customlabel{clause:lam-lam}{\lambda\lambda}$)~
  o-uva N ~$\Eo$~ o-uva N.
  o-lam F ~$\Eo$~ T :-                                       ~($\customlabel{clause:eta1}{\eta_l}$)~
    pi x\ beta T [x] (T' x), x ~$\Eo$~ x => F x ~$\Eo$~ T' x.
  T ~$\Eo$~ o-lam F :-                                       ~($\customlabel{clause:eta2}{\eta_r}$)~
    pi x\ beta T [x] (T' x), x ~$\Eo$~ x => T' x ~$\Eo$~ F x.
  o-app [o-lam X|L] ~$\Eo$~ T :- beta (o-lam X) L R, R ~$\Eo$~ T. ~($\customlabel{clause:beta1}{\beta_l}$)~
  T ~$\Eo$~ o-app [o-lam X|L] :- beta (o-lam X) L R, T ~$\Eo$~ R. ~($\customlabel{clause:beta2}{\beta_r}$)~

  type extend-subst to -> fsubst -> fsubst -> o.
  extend-subst (o-uva N) S S' :- mem.alloc N S S'.
  extend-subst (o-lam F) S S' :-
    pi x\ (pi S\extend-subst x S S) => extend-subst (F x) S S'.
  extend-subst (o-con _) S S.
  extend-subst (o-app L) S S1 :- fold extend-subst L S S1.

  type beta to -> list to -> to -> o.
  beta A [] A.
  beta (o-lam Bo) [H | L] R :- napp (Bo H) F, beta F L R.
  beta (o-app A) L (o-app X) :- append A L X.
  beta (o-uva N) L (o-app [o-uva N | L]).
  beta (o-con H) L (o-app [o-con H | L]).
  beta N L (o-app [N | L]) :- name N.

  type napp to -> to -> o.
  napp (o-con C) (o-con C).
  napp (o-uva A) (o-uva A).
  napp (o-lam F) (o-lam G) :- pi x\ napp (F x) (G x).
  napp (o-app [o-app L1 |L2]) T :- !,
    append L1 L2 L3, napp (o-app L3) T.
  napp (o-app L) (o-app L1) :- map napp L L1.
  napp N N :- name N.

  type beta-normal to -> to -> o.
  beta-normal (uvar _ _) _ :- halt "Passed uvar to beta-normal".
  beta-normal A A :- name A.
  beta-normal (o-con A) (o-con A).
  beta-normal (o-uva A) (o-uva A).
  beta-normal (o-lam A) (o-lam B) :-
    pi x\ beta-normal (A x) (B x).
  beta-normal (o-app [o-lam B | L]) T2 :- !,
    beta (o-lam B) L T1, beta-normal T1 T2.
  beta-normal (o-app L) (o-app L1) :-
    map beta-normal L L1.

  type mk-app to -> list to -> to -> o.
  mk-app T L S :- beta T L S.

  type eta-contract to -> to -> o.
  eta-contract (o-con X) (o-con X).
  eta-contract (o-app L) (o-app L1) :- map eta-contract L L1.
  eta-contract (o-lam F) T :- eta-contract-aux [] (o-lam F) T.
  eta-contract (o-lam F) (o-lam F1) :-
    pi x\ eta-contract x x => eta-contract (F x) (F1 x).
  eta-contract (o-uva X) (o-uva X).
  eta-contract X X :- name X.

  type eta-contract-aux list to -> to -> to -> o.
  eta-contract-aux L (o-lam F) T :- 
    pi x\ eta-contract-aux [x|L] (F x) T. % also checks H Prefix does not see x
  eta-contract-aux L (o-app [H|Args]) T :- 
    rev L LRev, append Prefix LRev Args,
    if (Prefix = []) (T = H) (T = o-app [H|Prefix]).
\end{elpicode}
