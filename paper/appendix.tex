\section*{Appendix}

This appendix contains the entire code described in this paper.
The code can also be accessed at the URL: \url{https://github.com/FissoreD/paper-ho}

Note that \elpiIn{(a infix b) c d} de-sugars to \elpiIn{(infix) a b c d}.

Explain builtin name (can be implemented by loading name after each pi)

\section{The memory}

\input{code/mem}

\section{The object language}

\input{code/object\_language}

\section{The meta language}

\input{code/meta\_language}

\section{The compiler}

\input{code/comp}

\section{The progress function}

\begin{elpicode}
  
  macro @one :- s z.

  type contract-rigid list ho.tm -> ho.tm -> ho.tm -> o.
  contract-rigid L (ho.lam F) T :- 
    pi x\ contract-rigid [x|L] (F x) T. % also checks H Prefix does not see x
  contract-rigid L (ho.app [H|Args]) T :- 
    rev L LRev, append Prefix LRev Args,
    if (Prefix = []) (T = H) (T = ho.app [H|Prefix]).

  type progress-eta-link ho.tm -> ho.tm -> ho.subst -> ho.subst -> links -> o.
  progress-eta-link (ho.app _ as T) (ho.lam x\ _ as T1) H H1 [] :- !, 
    ({eta-expand T @one} ==l T1) H H1.
  progress-eta-link (ho.con _ as T) (ho.lam x\ _ as T1) H H1 [] :- !, 
    ({eta-expand T @one} ==l T1) H H1.
  progress-eta-link (ho.lam _ as T) T1 H H1 [] :- !, 
    (T ==l T1) H H1.
  progress-eta-link (ho.uva _ _ as X) T H H1 [] :- 
    contract-rigid [] T T1, !, (X ==l T1) H H1.
  progress-eta-link (ho.uva Ad _ as T1) T2 H H [@val-link-eta T1 T2] :- !, 
    if (ho.not_occ Ad H T2) true fail.

  type is-in-pf ho.tm -> o.
  is-in-pf (ho.app [ho.uva _ _ | _]) :- !, fail.
  is-in-pf (ho.lam B) :- !, pi x\ is-in-pf (B x).
  is-in-pf (ho.con _).
  is-in-pf (ho.app L) :- forall1 is-in-pf L.
  is-in-pf N :- name N.
  is-in-pf (ho.uva _ L) :- pattern-fragment L.

  type arity ho.tm -> nat -> o.
  arity (ho.con _) z.
  arity (ho.app L) A :- len L A.

  type occur-check-err ho.tm -> ho.tm -> ho.subst -> o.
  occur-check-err (ho.con _) _ _ :- !.
  occur-check-err (ho.app _) _ _ :- !.
  occur-check-err (ho.lam _) _ _ :- !.
  occur-check-err (ho.uva Ad _) T S :-
    not (ho.not_occ Ad S T).

  type progress-beta-link-aux ho.tm -> ho.tm -> 
          ho.subst -> ho.subst -> links -> o.
  progress-beta-link-aux T1 T2 S1 S2 [] :-  is-in-pf T2, !,
    (T1 ==l T2) S1 S2.
  progress-beta-link-aux T1 T2 S S [@val-link-beta T1 T2] :- !.

  type progress-beta-link ho.tm -> ho.tm -> ho.subst -> 
        ho.subst -> links -> o.
  progress-beta-link T (ho.app[ho.uva V Scope | L] as T2) S S2 [@val-link-beta T T2] :- 
    arity T Arity, len L ArgsNb, ArgsNb >n Arity, !,
    minus ArgsNb Arity Diff, mem.new S V1 S1,
    eta-expand (ho.uva V1 Scope) Diff T1,
    ((ho.uva V Scope) ==l T1) S1 S2.

  progress-beta-link (ho.uva _ _ as T) (ho.app[ho.uva Ad1 Scope1 | L1] as T1) S1 S3 NewLinks :-
    append Scope1 L1 Scope1L,
    pattern-fragment-prefix Scope1L Scope2 L2,
    not (Scope1 = Scope2), !,
    mem.new S1 Ad2 S2,
    len Scope1 Scope1Len,
    len Scope2 Scope2Len,
    make-eta-link Scope1Len Scope2Len Ad1 Ad2 [] LinkEta S2 S3,
    if (L2 = []) (NewLinks = LinkEta, T2 = ho.uva Ad2 Scope2) 
      (T2 = ho.app [ho.uva Ad2 Scope2 | L2], 
      NewLinks = [@val-link-beta T T2 | LinkEta]).

  progress-beta-link T1 (ho.app[ho.uva _ _ | _] as T2) _ _ _ :- 
    not (T1 = ho.uva _ _), !, fail.

  progress-beta-link (ho.uva _ _ as T) (ho.app[ho.uva _ _ | _] as T2) S1 _ _ :- 
    occur-check-err T T2 S1, !, fail.

  progress-beta-link T1 (ho.app[ho.uva _ _ | _] as T2) H H [@val-link-beta T1 T2] :- !.

  progress-beta-link T1 (ho.app [Hd | Tl]) S1 S2 B :-
    ho.beta Hd Tl T3, 
    progress-beta-link-aux T1 T3 S1 S2 B.

  type solve-link-abs link -> links -> ho.subst -> ho.subst -> o.
  solve-link-abs (ho.abs X) R H H1 :- 
    pi x\ ho.copy x x => (pi S\ ho.deref S x x) => 
      solve-link-abs (X x) (R' x) H H1,
    close-links R' R.

  solve-link-abs (@val-link-eta A B) NewLinks S S1 :- !,
    progress-eta-link A B S S1 NewLinks.

  solve-link-abs (@val-link-beta A B) NewLinks S S1 :- !,
    progress-beta-link A B S S1 NewLinks.

  type take-link link -> links -> link -> links -> o.
  take-link A [B|XS] B XS :- link-abs-same-lhs A B, !.
  take-link A [L|XS] B [L|YS] :- take-link A XS B YS.

  type link-abs-same-lhs link -> link -> o.
  link-abs-same-lhs (ho.abs F) B :- 
    pi x\ link-abs-same-lhs (F x) B.
  link-abs-same-lhs A (ho.abs G) :- 
    pi x\ link-abs-same-lhs A (G x).
  link-abs-same-lhs (@val-link-eta (ho.uva N _) _) (@val-link-eta (ho.uva N _) _).

  type same-link-eta link -> link -> ho.subst -> ho.subst -> o.
  same-link-eta (ho.abs F) B H H1 :- !, pi x\ same-link-eta (F x) B H H1.
  same-link-eta A (ho.abs G) H H1 :- !, pi x\ same-link-eta A (G x) H H1.
  same-link-eta (@val-link-eta (ho.uva N S1) A)
                (@val-link-eta (ho.uva N S2) B) H H1 :-
    std.map2 S1 S2 (x\y\r\ r = ho.copy x y) Perm,
    Perm => ho.copy A A',
    (A' ==l B) H H1.

  type solve-links links -> links -> ho.subst -> ho.subst -> o.
  solve-links [] [] X X.
  solve-links [A|L1] [A|L3] S S2 :- take-link A L1 B L2, !,
    same-link-eta A B S S1, 
    solve-links L2 L3 S1 S2.
  solve-links [L0|L1] L3 S S2 :- deref-link S L0 L,
    solve-link-abs L R S S1, !,
    solve-links L1 L2 S1 S2, append R L2 L3.
\end{elpicode}

\section{The decompiler}

\begin{elpicode}
  
  type abs->lam ho.assignment -> ho.tm -> o.
  abs->lam (ho.abs T) (ho.lam R)  :- !, pi x\ abs->lam (T x) (R x).
  abs->lam (ho.val A) A.

  type commit-links-aux link -> ho.subst -> ho.subst -> o.
  commit-links-aux (@val-link-eta T1 T2) H1 H2 :- 
    ho.deref H1 T1 T1', ho.deref H1 T2 T2',
    (T1' ==l T2') H1 H2.
  commit-links-aux (@val-link-beta T1 T2) H1 H2 :- 
    ho.deref H1 T1 T1', ho.deref H1 T2 T2',
    (T1' ==l T2') H1 H2.
  commit-links-aux (ho.abs B) H H1 :- 
    pi x\ commit-links-aux (B x) H H1.

  type commit-links links -> links -> ho.subst -> ho.subst -> o.
  commit-links [] [] H H.
  commit-links [Abs | Links] L H H2 :- 
    commit-links-aux Abs H H1, !, commit-links Links L H1 H2.

  type decompl-subst map -> map -> ho.subst -> 
    fo.subst -> fo.subst -> o.
  decompl-subst _ [A|_] _ _ _ :- fail.
  decompl-subst _ [] _ F F.
  decompl-subst Map [mapping (fv VO) (hv VM _)|Tl] H F F2 :- 
    mem.set? VM H T, !, 
    ho.deref-assmt H T TTT,
    abs->lam TTT T', tm->fm Map T' T1, 
    fo.eta-contract T1 T2, mem.assign VO F T2 F1,
    decompl-subst Map Tl H F1 F2. 
  decompl-subst Map [mapping _ (hv VM _)|Tl] H F F2 :- 
    mem.unset? VM H, decompl-subst Map Tl H F F2.

  type tm->fm map -> ho.tm -> fo.fm -> o.
  tm->fm _ (ho.con C)  (fo.fcon C).
  tm->fm L (ho.lam B1) (fo.flam B2) :- 
    pi x y\ tm->fm _ x y => tm->fm L (B1 x) (B2 y).
  tm->fm L (ho.app L1) T :- map (tm->fm L) L1 [Hd|Tl], 
    fo.mk-app Hd Tl T.
  tm->fm L (ho.uva VM TL) T :- mem L (mapping (fv VO) (hv VM _)), 
    map (tm->fm L) TL T1, fo.mk-app (fo.fuva VO) T1 T.

  type add-new-map-aux ho.subst -> list ho.tm -> map -> 
        map ->  fo.subst -> fo.subst -> o.
  add-new-map-aux _ [] _ [] S S.
  add-new-map-aux H [T|Ts] L L2 S S2 :- 
    add-new-map H T L L1 S S1, 
    add-new-map-aux H Ts L1 L2 S1 S2.

  type add-new-map ho.subst -> ho.tm -> map -> 
      map ->  fo.subst -> fo.subst -> o.
  add-new-map _ (ho.uva N _) Map [] F1 F1 :- 
    mem Map (mapping _ (hv N _)), !.
  add-new-map H (ho.uva N L) Map [Map1 | MapL] F1 F3 :-
    mem.new F1 M F2,
    len L Arity, Map1 = mapping (fv M) (hv N (arity Arity)),
    add-new-map H (ho.app L) [Map1 | Map] MapL F2 F3.
  add-new-map H (ho.lam B) Map NewMap F1 F2 :- 
    pi x\ add-new-map H (B x) Map NewMap F1 F2.
  add-new-map H (ho.app L) Map NewMap F1 F3 :- 
    add-new-map-aux H L Map NewMap F1 F3.
  add-new-map _ (ho.con _) _ [] F F :- !.
  add-new-map _ N _ [] F F :- name N.

  type complete-mapping-under-ass ho.subst -> ho.assignment -> 
    map -> map ->  fo.subst -> fo.subst -> o.
  complete-mapping-under-ass H (ho.val Val) Map1 Map2 F1 F2 :- 
    add-new-map H Val Map1 Map2 F1 F2.
  complete-mapping-under-ass H (ho.abs Abs) Map1 Map2 F1 F2 :- 
    pi x\ complete-mapping-under-ass H (Abs x) Map1 Map2 F1 F2.

  type complete-mapping ho.subst -> ho.subst ->   
    map -> map -> fo.subst -> fo.subst -> o.
  complete-mapping _ [] L L F F.
  complete-mapping H [none | Tl] L1 L2 F1 F2 :-   
    complete-mapping H Tl L1 L2 F1 F2.
  complete-mapping H [some T0 | Tl] L1 L3 F1 F3 :-
    ho.deref-assmt H T0 T,
    complete-mapping-under-ass H T L1 L2 F1 F2, 
    append L1 L2 LAll,
    complete-mapping H Tl LAll L3 F2 F3.

  type decompile map -> links -> ho.subst -> 
    fo.subst -> fo.subst -> o.
  decompile Map1 L HO FO FO2 :- 
    commit-links L L1_ HO HO1, !,
    complete-mapping HO1 HO1 Map1 Map2 FO FO1,
    decompl-subst Map2 Map2 HO1 FO1 FO2.
\end{elpicode}

\section{Auxiliary functions}

\begin{elpicode}
  type fold4 (A -> A1 -> B -> B -> C -> C -> o) -> list A -> 
    list A1 -> B -> B -> C -> C -> o.
  fold4 _ [] [] A A B B.
  fold4 F [X|XS] [Y|YS] A A1 B B1 :- F X Y A A0 B B0, 
    fold4 F XS YS A0 A1 B0 B1.
  
  type len list A -> nat -> o.
  len [] z.
  len [_|L] (s X) :- len L X.
  
  \end{elpicode}
  