type fatal any -> o.
type fatal any -> any -> o.
type fatal any -> any -> any -> o.
type fatal any -> any -> any -> any -> o.
type fatal any -> any -> any -> any -> any -> o.
fatal A :- failsafe, !, print A, fail.
fatal A B :- failsafe, !, print A B, fail.
fatal A B C :- failsafe, !, print A B C, fail.
fatal A B C D :- failsafe, !, print A B C D, fail.
fatal A B C D E :- failsafe, !, print A B C D E, fail.
fatal A :- halt A.
fatal A B :- halt A B.
fatal A B C :- halt A B C.
fatal A B C D :- halt A B C D.
fatal A B C D E :- halt A B C D E.
type failsafe o.

kind nat type.
type z nat.
type s nat -> nat.

kind bool type.
type tt bool.
type ff bool.

kind option type -> type.
type some A -> option A.
type none option A.

type nth nat -> list A -> A -> o.
nth z (X :: _) Y  :- X = Y.
nth (s N) (_ :: L) X :- nth N L X.

type not-in A -> list A -> o.
not-in _ [].
not-in T [X | TL] :- not (X = T), not-in T TL.

type exists (A -> o) -> list A -> o.
exists F [X|XS] :- F X ; exists F XS.

type forall2 (A -> B -> o) -> list A -> list B -> o.
forall2 _ [] [].
forall2 F [X|XS] [Y|YS] :- F X Y, forall2 F XS YS.

type fold2 (A -> A1 -> B -> B -> o) -> list A -> list A1 -> B -> B -> o.
fold2 _ [] [] A A.
fold2 F [X|XS] [Y|YS] A A1 :- F X Y A A0, fold2 F XS YS A0 A1.

type fold (A -> B -> B -> o) -> list A -> B -> B -> o.
fold _ [] A A.
fold F [X|XS] A A1 :- F X A A0, fold F XS A0 A1.

type (>n) nat -> nat -> o.
s _ >n z.
s N >n s M :- N >n M.

type (>=n) nat -> nat -> o.
_ >=n z.
s N >=n s M :- N >=n M.

type diff nat -> nat -> o.
diff N M :- N >n M ; M >n N.

type map-fold2 (A -> A1 -> B -> B -> C -> C -> o) -> list A -> B -> C -> list A1 -> B -> C -> o.
map-fold2 _F [] B C [] B C.
map-fold2 F [X | XS] B C [Y | YS] B2 C2 :-
  F X Y B B1 C C1,
  map-fold2 F XS B1 C1 YS B2 C2.

type mem (list A) -> A -> o.
mem [A|_] A :- !.
mem [_|L] A :- mem L A.


type appendR list A -> list A -> list A -> o.
appendR [] A A.
appendR [A | TL] L [A | L'] :- appendR TL L L'.

type fold4 (A -> A1 -> B -> B -> C -> C -> o) -> list A -> list A1 -> B -> B -> C -> C -> o.
fold4 _ [] [] A A B B.
fold4 F [X|XS] [Y|YS] A A1 B B1 :- F X Y A A0 B B0, fold4 F XS YS A0 A1 B0 B1.

type fold6 (A -> A1 -> B -> B -> C -> C -> D -> D -> o) -> list A -> list A1 -> B -> B -> C -> C -> D -> D -> o.
fold6 _ [] [] A A B B C C.
fold6 F [X|XS] [Y|YS] A A1 B B1 C C1 :- F X Y A A0 B B0 C C0, fold6 F XS YS A0 A1 B0 B1 C0 C1.

type all list o -> o.
all [].
all [P|PS] :- P, all PS.

type forall1 (X -> o) -> list X -> o.
forall1 _ L :- var L, !.
forall1 P [X|XS] :- P X, forall1 P XS.
forall1 _ [].

type len list A -> nat -> o.
len [] z.
len [_|L] (s X) :- len L X.

% [split-pf L [] ]
type split-pf-aux list A -> list A -> list A -> list A -> o.
split-pf-aux [] _ [] [].
split-pf-aux [X | L] ACC [X | PF] NPF :- name X, not-in X ACC, !, 
  split-pf-aux L [X | ACC] PF NPF.
split-pf-aux L _ [] L.

type split-pf list A -> list A -> list A -> o.
split-pf L PF NPF :- split-pf-aux L [] PF NPF.

pred pattern-fragment i:list A.
pattern-fragment L :- distinct_names L.

type minus nat -> nat -> nat -> o.
minus z _ z.
minus A z A.
minus (s N) (s M) R :- minus N M R.  

type last list A -> A -> o.
last [A] A.
last [_|L] A :- last L A.

type drop-first-n nat -> list A -> list A -> o.
drop-first-n z L L.
drop-first-n (s N) [_|As] Bs :- drop-first-n N As Bs. 

type last-n nat -> list A -> list A -> o.
last-n N L R :- minus {len L} N PrefLen, drop-first-n PrefLen L R.

kind test type -> type.
type test int -> (pair (list (triple diagnostic A A)) (option(list A))) -> test A.
type declare-test (test A -> o) -> (test A -> string -> o) -> test A -> o.

kind triple type -> type -> type -> type.
type triple A -> B -> C -> triple A B C.

type runtests list o -> list int -> o.
runtests [] [] :- print "ALL GOOD!".
runtests [] F :- print "SOME TEST FAIL:" F, fail.
runtests [declare-test F PP (test N _ as T) |TS] ERR :- print "\n-----" N "-------\n\n\n",
  PP T Tstr,
  if (F T) (print "ok" Tstr, runtests TS ERR)
           (print "\n<<<<<\nko" Tstr "\n>>>>>\n", runtests TS [N|ERR]).

type internal-tests o.
type internal-test o -> o.

internal-tests :-
  std.findall (internal-test _) L,
  forall1 runinternal L, !.

type runinternal o -> o.
runinternal (internal-test P) :- print "######" P, P, !.
runinternal (internal-test P) :- halt "fail internal test" P.

type ppnat-aux nat -> int -> o.
ppnat-aux z 0.
ppnat-aux (s N) M :- ppnat-aux N M1, M is M1 + 1.

type ppnat nat -> string -> o.
ppnat X _ :- var X, halt "ppnat".
ppnat N S :- ppnat-aux N M, std.any->string M S.