internal-test fo.test-parse-1.

namespace fo {
  shorten mem.{ set? , unset? }.
%BEGIN

%SNIP: fo_tm
  kind to type.
  type o-app list to -> to.
  type o-lam (to -> to) -> to.
  type o-con string -> to.
  type o-uva addr -> to.
%ENDSNIP

%SNIP: fo_subst
  typeabbrev fsubst (mem to).
%ENDSNIP

%SNIP: fderef
  type fder fsubst -> to -> to -> o.
  fder S X _ :- (var S; var X), fatal "flex fder". % HIDE
  fder _ (o-con C) (o-con C).
  fder S (o-app A) (o-app B) :- map (fder S) A B.
  fder S (o-lam F) (o-lam G) :-
    pi x\ fder S x x => fder S (F x) (G x).
  fder S (o-uva N) R :- set? N S T, fder S T R.
% fder S (o-app [o-uva N|L]) R :- set? N S T, !, beta T L R', fder S R' R.
  fder S (o-uva N) (o-uva N) :- unset? N S.
% fder _ N N :- name N.

  type fderef fsubst -> to -> to -> o.                 %~($\customlabel{pred:fder}{\rho s}$)~
  fderef S T T2 :- fder S T T1, napp T1 T2.
%ENDSNIP


%SNIP: feq
  type (==o) to -> to -> o.                              %~($\customlabel{pred:fequal}{\Eo}$)~
  X ==o Y :- (var X ; var Y), fatal "flex equal". % HIDE
  o-con X ==o o-con X.
  o-app A ==o o-app B :- forall2 (==o) A B.
  o-lam F ==o o-lam G :- pi x\ x ==o x => F x ==o G x.    %~($\customlabel{clause:lam-lam}{\lambda\lambda}$)~
  o-uva N ==o o-uva N.
  o-lam F ==o T :-                                       %~($\customlabel{clause:eta1}{\eta_l}$)~
    pi x\ beta T [x] (T' x), x ==o x => F x ==o T' x.
  T ==o o-lam F :-                                       %~($\customlabel{clause:eta2}{\eta_r}$)~
    pi x\ beta T [x] (T' x), x ==o x => T' x ==o F x.
  o-app [o-lam X|L] ==o T :- beta (o-lam X) L R, R ==o T. %~($\customlabel{clause:beta1}{\beta_l}$)~
  T ==o o-app [o-lam X|L] :- beta (o-lam X) L R, T ==o R. %~($\customlabel{clause:beta2}{\beta_r}$)~
%ENDSNIP

  % [extend-subst T S S'] forall o-uva N in T, extends S with none at pos N
  % this is used only in test to build the subst-map before running it
  type extend-subst to -> fsubst -> fsubst -> o.
  extend-subst (o-uva N) S S' :- mem.alloc N S S'.
  extend-subst (o-lam F) S S' :-
    pi x\ (pi S\extend-subst x S S) => extend-subst (F x) S S'.
  extend-subst (o-con _) S S.
  extend-subst (o-app L) S S1 :- fold extend-subst L S S1.

%SNIP: beta_fo
  type beta to -> list to -> to -> o.
  beta A [] A.
  beta (o-lam Bo) [H | L] R :- napp (Bo H) F, beta F L R.
  beta (o-app A) L (o-app X) :- append A L X.
  beta (o-uva N) L (o-app [o-uva N | L]).
  beta (o-con H) L (o-app [o-con H | L]).
  beta N L (o-app [N | L]) :- name N.

  type napp to -> to -> o.
  napp (o-con C) (o-con C).
  napp (o-uva A) (o-uva A).
  napp (o-lam F) (o-lam G) :- pi x\ napp (F x) (G x).
  napp (o-app [o-app L1 |L2]) T :- !,
    append L1 L2 L3, napp (o-app L3) T.
  napp (o-app L) (o-app L1) :- map napp L L1.
  napp N N :- name N.
%ENDSNIP

  type beta-normal to -> to -> o.
  beta-normal (uvar _ _) _ :- halt "Passed uvar to beta-normal".
  beta-normal A A :- name A.
  beta-normal (o-con A) (o-con A).
  beta-normal (o-uva A) (o-uva A).
  beta-normal (o-lam A) (o-lam B) :-
    pi x\ beta-normal (A x) (B x).
  beta-normal (o-app [o-lam B | L]) T2 :- !,
    beta (o-lam B) L T1, beta-normal T1 T2.
  beta-normal (o-app L) (o-app L1) :-
    map beta-normal L L1.

  type mk-app to -> list to -> to -> o.
  mk-app T L S :- beta T L S.

  type eta-contract to -> to -> o.
  eta-contract X _ :- var X, halt "Eta-contract first args is flexible". % HIDE
  eta-contract (o-con X) (o-con X).
  eta-contract (o-app L) (o-app L1) :- map eta-contract L L1.
  eta-contract (o-lam F) T :- eta-contract-aux [] (o-lam F) T.
  eta-contract (o-lam F) (o-lam F1) :-
    pi x\ eta-contract x x => eta-contract (F x) (F1 x).
  eta-contract (o-uva X) (o-uva X).
  eta-contract X X :- name X.

  type eta-contract-aux list to -> to -> to -> o.
  eta-contract-aux L (o-lam F) T :- 
    pi x\ eta-contract-aux [x|L] (F x) T. % also checks H Prefix does not see x
  eta-contract-aux L (o-app [H|Args]) T :- 
    rev L LRev, append Prefix LRev Args,
    if (Prefix = []) (T = H) (T = o-app [H|Prefix]).
%END
  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% PRINTER %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

  macro @binder-names :- ["x", "y", "z", "w"].
  macro @o-uva N :- o-uva (mem.addr {int->nat N}).

  % type pp-var-clean string -> string -> o.
  % pp-var-clean S1 S2 :-
  %   std.any->string (@o-uva 0) T1,
  %   std.any->string (@o-uva 1) T2,
  %   std.any->string (@o-uva 2) T3,
  %   std.any->string (@o-uva 3) T4,
  %   L1 = [T1,T2,T3,T4, "c0", "c1", "c2", "c3"],
  %   L2 = ["o-uva X", "o-uva Y", "o-uva Z", "o-uva W", "x", "y", "z", "w"],
  %   std.map2 L1 L2 (x\y\z\z = pr x y) L3,
  %   fold (x\a\r\ sigma A B\ x = pr A B, rex.replace A B a r) L3 S1 S2.

  type lambda string -> o.
  lambda "\\lambda " :- tex, !.
  lambda "λ".

  type appsep string -> o.
  appsep "\\appsep " :- tex, !.
  appsep " ".

  type pp to -> string -> o.
  % pp T S :- tex, !, 
  %   std.any->string T S1, 
  %   pp-var-clean S1 S2,
  %   rex.replace "fo." "" S2 S3,
  %   rex.replace " \\\\" "\\\\" S3 S4,
  %   rex.replace "o-con \(.\)" "o-con\"\1\"" S4 S.
  pp T S :- pp1 @binder-names T S.

  type pp1 list string -> to -> string -> o.
  pp1 _ X "_" :- var X, !.
  pp1 XS (o-app L) S :- !, std.string.concat {appsep} {std.map L (pp1 XS)} X, S is "(" ^ X ^ ")".
  pp1 _ (o-con S) S :- !.
  pp1 [] (o-lam F) S :- !, pi x\ std.any->string x N, pp1 [] (F x) S1, S is {lambda} ^ N ^ "." ^ S1.
  pp1 [X|XS] (o-lam F) S :- !, pi x\ (pp1 _ x X :- !) => pp1 XS (F x) S1, S is {lambda} ^ X ^ "." ^ S1.
  pp1 _ (o-uva N) S :- !, ppuva N S.
  pp1 _ X S :- name X, !, std.any->string X S.
  pp1 _ _ "fo.pp1 ERROR".

  type ppuva addr -> string -> o.
  ppuva (mem.addr N) S :- 
    nth N ["X", "Y", "Z", "W"] S, !.
  ppuva (mem.addr N) S :- 
    ppnat N X, S is "C" ^ X.

  type ppsubst fsubst -> list string -> o.
  ppsubst L S :- mem.pp L ppuva (x\s\sigma y\pp x y, s is ":= " ^ y)  S.

  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% PARSER %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

  type parse string -> to -> o.
  parse S T :- rex.split "" S L, utf8-norm L L1, if (parse-aux L1 T) true (halt "Error parse" S).

  type parse-aux list string -> to -> o.
  parse-aux L1 T :- ll L1 [] T, !.
  parse-aux L1 T :- llapp L1 [] [] A, T = o-app A, !.
  parse-aux L1 _ :- std.string.concat "" L1 S, halt "parse error:" S. 

  type parse-eq string -> to -> to -> o.
  parse-eq S A B  :- rex.split "" S L, utf8-norm L L1, if (parse-eq-aux L1 A B) true (halt "Error parse-eq" S).

  type parse-eq-aux list string -> to -> to -> o.
  parse-eq-aux L1 A B :-
    llapp L1 L2 [] AA, if (AA = [A]) true (A = o-app AA), !,
    ee L2 L3,
    llapp L3 [] [] BB, if (BB = [B]) true (B = o-app BB).

  type ee list string -> list string -> o.
  ee ["="|L] L.
  ee [" "|L] L1 :- ee L L1.

  type ll list string -> list string -> to -> o.
  ll [C|L] L (o-con C) :- rex.match "[a-z]" C.
  ll ["_"|L] L (o-con "_").
  ll ["C",N|L] L (o-uva (mem.addr R)) :- rex.match "[0-9]" N, N' is string_to_int N, int->nat N' R. 
  ll ["X"|L] L (o-uva (mem.addr z)).
  ll ["Y"|L] L (o-uva (mem.addr (s z))).
  ll ["Z"|L] L (o-uva (mem.addr (s (s z)))).
  ll ["W"|L] L (o-uva (mem.addr (s (s (s z))))).
  ll [ C |_] _ _ :- rex.match "[A-Z]" C, halt "Parser:" C "is not parsed as a knwon variable".
  ll ["(","@"|L] L1 T :- !, llapp L L1 [] A, if (A = [T]) true (T = o-app A).
  ll ["("|L] L1 T :- llapp L L1 [] A, if (A = [T]) true (T = o-app A).
  ll ["λ",V,"."|L] L1 (o-lam X) :- pi x\ (pi L\ ll [V|L] L x :- !) => ll L L1 (X x).
  ll [" "|L] L1 X :- ll L L1 X.

  type llapp list string -> list string -> list to -> list to -> o.
  llapp [")"|L] L A RA :- !, rev A RA.
  llapp L L2 A B :- ll L L1 X, !, llapp L1 L2 [X|A] B.
  llapp L L A RA :- rev A RA.

  type utf8-norm list string -> list string -> o.
  utf8-norm [X,Y|L] ["λ"|L1] :- "λ" is X ^ Y, !, utf8-norm L L1.
  utf8-norm [X|L] [X|L1] :- utf8-norm L L1.
  utf8-norm [] [].

  type test-parse-1 o.
  test-parse-1 :-
    parse "C0" (o-uva (mem.addr z)),
    parse "f" (o-con "f"),
    parse "(f)" (o-con "f"),
    parse "(f g)" (o-app [o-con "f", o-con "g"]),
    parse "λx.x" (o-lam x\x).

}
